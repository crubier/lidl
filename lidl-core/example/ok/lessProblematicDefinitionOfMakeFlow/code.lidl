interaction
  (bob):{theNumber:Number in,theResult:Number out}
with

  interaction
    (previous (x:Number in)):Number out
  is
    (
      (variable previous(x))
      with behaviour
      (get(variable previous(x))from previous and set (x) for next)
    )

  interaction
    (not(a:Boolean in)):Boolean out
  with
    interaction (boolean negation):{Boolean->Boolean} out is (function boolNot)
  is
    (
      (variable not (a))
      with behaviour
      (apply (boolean negation) to (a) and get (variable not (a)) )
    )

  interaction
    ((a:Number in) is active):Boolean out
  with
    interaction (is active):{Number->Boolean}out is (function isActive)
  is
    (
      (variable  (a) is active)
      with behaviour
      (apply (is active) to (a) and get (variable  (a) is active) )
    )

  interaction
    ((a:Number in)+(b:Number in)):Number out
  with
    interaction (addition):{{a:Number,b:Number}->Number} out is (function addition)
  is
    (
      (variable result of (a)+(b))
      with behaviour
      (
        apply
        (addition)
        to
        ({a:(a)b:(b)})
        and get
        (variable result of (a)+(b))
      )
    )


  interaction
    (init):Boolean out
  is
    ( not ( (previous(1) ) is active ) )

  interaction
    (all (a:Activation out) else (b:Activation out)):Activation in
  is
    (
      (variable all (a) (b))
      with behaviour
      (
        apply
        (function all)
        to
        (variable all (a) (b))
        and get
        ({a:(a) b:(b)})
      )
    )

  interaction
    ( (a:Number in) fallback to (b:Number in)):Number out
  is
    (
      if ((a) is active)
      then (a)
      else (b)
    )

  interaction
    ( (a:Number in) fallback to (b:Number in) fallback to (c:Number in)):Number out
  is
    (((a) fallback to (b)) fallback to (c))

  interaction
    (new (x:Number in)):Number in
  is
    (variable new (x))

  interaction
    ((u:Number in)==(v:Number in)):Boolean out
  with
    interaction (is equal):{{a:Number,b:Number}->Boolean}out is (function isEqual)
  is
    (
      (variable result of (u)==(v))
      with behaviour
      (apply (is equal) to ({a:(u),b:(v)}) and get (variable result of (u)==(v)) )
    )


  interaction
    (when (cond:Boolean in) then (a:Activation out) else (b:Activation out)):Activation in
  with
    interaction
      (whenThenElse):{{cond:Boolean,source:Activation}->{a:Activation,b:Activation}}out
    is
      (function whenThenElse)
  is
    (
      (variable activation of when (cond) then (a) else (b))
      with behaviour
      (
        apply
        (whenThenElse)
        to
        ({  cond:(cond)  source:(variable activation of when (cond) then (a) else (b))   })
        and get
        ({a:(a) b:(b)})
      )
    )

  interaction
    (if (cond:Boolean in) then (x:Number in) else (y:Number in)):Number out
  is
    (
      (variable result of if (cond) then (x) else (y))
      with behaviour
      (
        when
        (cond)
        then
        ((variable result of if (cond) then (x) else (y)) = (x))
        else
        ((variable result of if (cond) then (x) else (y)) = (y))
      )
    )


is
  (
    ({
      theNumber:(new (variable theNumber))
      theResult:(variable theNumber)
    })
    with behaviour
    ( (variable theNumber)  =   (  (new (variable theNumber)) fallback to  (if (init) then (1) else (previous (variable theNumber) ) ) ))
  )
